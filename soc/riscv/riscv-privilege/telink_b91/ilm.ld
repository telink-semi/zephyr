/*
 * Copyright (c) 2023 Telink Semiconductor
 *
 * SPDX-License-Identifier: Apache-2.0
 */


#ifdef CONFIG_BOARD_TLSR9518ADK80D
#define TELINK_RAM_REGION              RAMILM
#define TELINK_RAM_CODE_REGION         RAMILM
#endif /* CONFIG_BOARD_TLSR9518ADK80D */

#ifdef CONFIG_BOARD_TLSR9518ADK80D_RETENTION
#define TELINK_RAM_REGION              RAMABLE_REGION
#ifdef CONFIG_BOARD_TLSR9518ADK80D_NON_RETENTION_RAM_CODE
#define TELINK_RAM_CODE_REGION         RAM_ILM_N
#else
#define TELINK_RAM_CODE_REGION         RAMABLE_REGION
#endif /* CONFIG_BOARD_TLSR9518ADK80D_NON_RETENTION_RAM_CODE */
#endif /* CONFIG_BOARD_TLSR9518ADK80D_RETENTION */

SECTION_DATA_PROLOGUE(retention_reset,,)
{
	. = ALIGN(4);
	KEEP(*(.retention_reset.*))

	PROVIDE (_RETENTION_RESET_VMA_END = .);
	PROVIDE (_RETENTION_RESET_VMA_START = ADDR(retention_reset));
	PROVIDE (_RETENTION_RESET_LMA_START = LOADADDR(retention_reset));
} GROUP_DATA_LINK_IN(TELINK_RAM_REGION, ROMABLE_REGION)

SECTION_DATA_PROLOGUE(aes_data,,)
{
	. = ALIGN(4);
	*(.aes_data)
	*(".aes_data.*")

	PROVIDE (_AES_DATA_VMA_END = .);
	PROVIDE (_AES_DATA_VMA_START = ADDR(aes_data));
} GROUP_DATA_LINK_IN(TELINK_RAM_REGION, ROMABLE_REGION)

SECTION_DATA_PROLOGUE(retention_data,,)
{
	. = ALIGN(4);
	*(.retention_data)
	*(".retention_data.*")

	PROVIDE (_RETENTION_DATA_VMA_END = .);
	PROVIDE (_RETENTION_DATA_VMA_START = ADDR(retention_data));
	PROVIDE (_RETENTION_DATA_LMA_START = LOADADDR(retention_data));
} GROUP_DATA_LINK_IN(TELINK_RAM_REGION, ROMABLE_REGION)

SECTION_DATA_PROLOGUE(ram_code,,)
{
	. = ALIGN(4);
	*(.ram_code)
	*(".ram_code.*")

	PROVIDE (_RAMCODE_VMA_END = .);
	PROVIDE (_RAMCODE_VMA_START = ADDR(ram_code));
	PROVIDE (_RAMCODE_LMA_START = LOADADDR(ram_code));
} GROUP_DATA_LINK_IN(TELINK_RAM_CODE_REGION, ROMABLE_REGION)

SECTION_DATA_PROLOGUE(noinit_ilm, (NOLOAD),)
{
	. = ALIGN(4);
	*(.noinit)
	*(".noinit.*")
} GROUP_DATA_LINK_IN(TELINK_RAM_REGION, ROMABLE_REGION)
